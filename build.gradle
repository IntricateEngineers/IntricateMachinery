buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

plugins {
      id 'org.ensime.gradle' version '0.2.8'
}
apply plugin: 'scala'
apply plugin: 'net.minecraftforge.gradle.forge'

def parseprops(File cfg) {
    cfg.withReader {
        def prop = new Properties()
        prop.load(it)
        return (new ConfigSlurper().parse(prop))
    }
}
ext.ref = parseprops(file('build.properties'))
ext.ref.mod_deps = parseprops(file('mod_deps.properties'))

group = ref.package_base + '.' + ref.mod_id
archivesBaseName = ref.mod_id
version = (System.getenv("TRAVIS") ? "1.9-DEV-" + System.getenv('TRAVIS_BUILD_NUMBER') : ref.mc_version + '-' + ref.mod_version)
def versionName = (System.getenv("TRAVIS") ? "1.9-" + ref.mod_version + "-DEV-" +  System.getenv('TRAVIS_BUILD_NUMBER') : version)

minecraft {
    version = ref.mcf_version
    runDir = ref.run_dir
    mappings = ref.mappings
    replace '@MOD_NAME@', ref.mod_name
    replace '@MOD_ID@', ref.mod_id
    replace '@MOD_VERSION@', versionName
    replace '@MCF_VERSION@', ref.mcf_version
    if (ref.mod_deps!="") replace '@MOD_DEPS@', ";" + ref.mod_deps.depstring
    else replace '@MOD_DEPS@', ''
    replaceIn 'ModInfo.java'
}

repositories {
    maven {
        // Tesla
        url 'http://maven.epoxide.xyz'
    }
    maven {
        // MCMultipart
        url "http://maven.amadornes.com"
    }
    maven {
        name = "mojang"
        url = "https://libraries.minecraft.net/"
    }
}

dependencies {
    compile "net.darkhax.tesla:Tesla:1.9.4-1.1.0.24"
    deobfCompile "MCMultiPart:MCMultiPart-experimental:1.2.0_71:deobf"
    compile "org.scala-lang:scala-library:2.11.8"

    // Topi needs this shit
    compile "oshi-project:oshi-core:1.1"
    compile "net.java.dev.jna:jna:3.4.0"
    compile "net.java.dev.jna:platform:3.4.0"
    compile "com.ibm.icu:icu4j-core-mojang:51.2"
    compile "net.sf.jopt-simple:jopt-simple:4.6"
    compile "com.paulscode:codecjorbis:20101023"
    compile "com.paulscode:codecwav:20101023"
    compile "com.paulscode:libraryjavasound:20101123"
    compile "com.paulscode:librarylwjglopenal:20100824"
    compile "com.paulscode:soundsystem:20120107"
    compile "io.netty:netty-all:4.0.23.Final"
    compile "com.google.guava:guava:17.0"
    compile "org.apache.commons:commons-lang3:3.3.2"
    compile "commons-io:commons-io:2.4"
    compile "commons-codec:commons-codec:1.9"
    compile "net.java.jinput:jinput:2.0.5"
    compile "net.java.jutils:jutils:1.0.0"
    compile "com.google.code.gson:gson:2.2.4"
    compile "com.mojang:authlib:1.5.22"
    compile "com.mojang:realms:1.8.19"
    compile "org.apache.commons:commons-compress:1.8.1"
    compile "org.apache.httpcomponents:httpclient:4.3.3"
    compile "commons-logging:commons-logging:1.1.3"
    compile "org.apache.httpcomponents:httpcore:4.3.2"
    compile "it.unimi.dsi:fastutil:7.0.12_mojang"
    compile "org.apache.logging.log4j:log4j-api:2.0-beta9"
    compile "org.lwjgl.lwjgl:lwjgl:2.9.4-nightly-20150209"
    compile "org.lwjgl.lwjgl:lwjgl_util:2.9.4-nightly-20150209"
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
		expand ([
			'modid':ref.mod_id,
			'name':ref.mod_name,
			'description':ref.mod_desc,
			'version':ref.mod_version,
			'mcversion':ref.mc_version,
			'url':ref.mod_url,
			'authorList':ref.mod_author,
			'credits':ref.mod_creds,
			'logoFile':ref.mod_logo,
		])
	}
	from(sourceSets.main.resources.srcDirs) {exclude 'mcmod.info'}
}
runClient {
    jvmArgs "-Xmx4G", "-Xms4G"
}
